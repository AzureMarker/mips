use crate::ast::*;
use std::str::FromStr;

grammar;

Comma<T>: Vec<T> = {
    <mut items:(<T> ",")*> <last:T?> => {
        items.extend(last);
        items
    }
};

pub Program: Program = {
    <items:("\n"* <Item>)*> "\n"* => Program { items }
};

Item: Item = {
    <ConstantDef> "\n" => Item::ConstantDef(<>),
    <Directive> "\n" => Item::Directive(<>),
    <Identifier> ":" => Item::Label(<>),
    <Instruction> "\n" => Item::Instruction(<>),
};

Expr: Expr = {
    #[precedence(level = "1")]
    Number => Expr::Number(<>),
    #[precedence(level = "1")]
    Identifier => Expr::Constant(<>),
    #[precedence(level = "1")]
    "(" <Expr> ")",
    #[precedence(level = "1")]
    "-" <expr:Expr> => Expr::Negated(Box::new(expr)),

    #[precedence(level = "2")]
    #[assoc(side = "left")]
    <left:Expr> <operation:ExprOperation2> <right:Expr> => Expr::Calculated {
        operation,
        left: Box::new(left),
        right: Box::new(right),
    },

    #[precedence(level = "3")]
    #[assoc(side = "left")]
    <left:Expr> <operation:ExprOperation3> <right:Expr> => Expr::Calculated {
        operation,
        left: Box::new(left),
        right: Box::new(right),
    },

    #[precedence(level = "4")]
    #[assoc(side = "left")]
    <left:Expr> <operation:ExprOperation4> <right:Expr> => Expr::Calculated {
        operation,
        left: Box::new(left),
        right: Box::new(right),
    },

    #[precedence(level = "5")]
    #[assoc(side = "left")]
    <left:Expr> "&" <right:Expr> => Expr::Calculated {
        operation: Operation::BitwiseAnd,
        left: Box::new(left),
        right: Box::new(right),
    },

    #[precedence(level = "6")]
    #[assoc(side = "left")]
    <left:Expr> "^" <right:Expr> => Expr::Calculated {
        operation: Operation::BitwiseXor,
        left: Box::new(left),
        right: Box::new(right),
    },

    #[precedence(level = "7")]
    #[assoc(side = "left")]
    <left:Expr> "|" <right:Expr> => Expr::Calculated {
        operation: Operation::BitwiseOr,
        left: Box::new(left),
        right: Box::new(right),
    },
};

ExprOperation2: Operation = {
    "*" => Operation::Multiply,
    "/" => Operation::Divide,
};

ExprOperation3: Operation = {
    "+" => Operation::Add,
    "-" => Operation::Subtract,
};

ExprOperation4: Operation = {
    ">>" => Operation::BitwiseShiftRight,
    "<<" => Operation::BitwiseShiftLeft
};

Number: i64 = r"[0-9]+" => i64::from_str(<>).unwrap();

Identifier: String = r"[a-zA-Z_][a-zA-Z_0-9]*" => <>.to_string();

String: String = r#""([^\\"]|\\.)*""# => {
    let s = <>;
    s[1..s.len()-1].to_string()
};

ConstantDef: ConstantDef = <name:Identifier> "=" <value:Expr> => ConstantDef { name, value };

Directive: Directive = {
    ".text" => Directive::Text,
    ".globl" <label:Identifier> => Directive::Global { label },
    ".data" => Directive::Data,
    ".align" <boundary:Expr> => Directive::Align { boundary },
    ".space" <size:Expr> => Directive::Space { size },
    ".word" <values:Comma<Expr>> => Directive::Word { values },
    ".asciiz" <string:String> => Directive::Asciiz { string },
};

Instruction: Instruction = {
    <op_code:RTypeOpRdRsRt> <rd:Register> "," <rs:Register> "," <rt:Register>
        => Instruction::RType { op_code, rd, rs, rt, shift: Expr::Number(0) },
    <op_code:RTypeOpRdRtRs> <rd:Register> "," <rt:Register> "," <rs:Register>
        => Instruction::RType { op_code, rd, rs, rt, shift: Expr::Number(0) },
    <op_code:RTypeOpRdRtSh> <rd:Register> "," <rt:Register> "," <shift:Expr>
        => Instruction::RType { op_code, rd, rs: Register::Number(0), rt, shift },
    <op_code:RTypeOpRsRt> <rs:Register> "," <rt:Register>
        => Instruction::RType { op_code, rd: Register::Number(0), rs, rt, shift: Expr::Number(0) },
    <op_code:RTypeOpRs> <rs:Register>
        => Instruction::RType { op_code, rs, rd: Register::Number(0), rt: Register::Number(0), shift: Expr::Number(0) },
    <op_code:RTypeOpRd> <rd:Register>
        => Instruction::RType { op_code, rs: Register::Number(0), rd, rt: Register::Number(0), shift: Expr::Number(0) },
    <op_code:ITypeOpRtRs> <rt:Register> "," <rs:Register> "," <immediate:Expr>
        => Instruction::IType { op_code, rt, rs, immediate },
    <op_code:ITypeOpRsRt> <rs:Register> "," <rt:Register> "," <immediate:Expr>
        => Instruction::IType { op_code, rs, rt, immediate },
    <op_data:ITypeOpRs> <rs:Register> "," <immediate:Expr>
        => Instruction::IType { op_code: op_data.0, rs, rt: Register::Number(op_data.1), immediate },
    <op_code:StorageITypeOp> <rt:Register> "," <immediate:Expr> "(" <rs:Register> ")"
        => Instruction::IType { op_code, rs, rt, immediate },
    <op_code:JTypeOp> <label:Expr>
        => Instruction::JType { op_code, label },
    "syscall" => Instruction::RType {
        op_code: RTypeOp::Syscall,
        rd: Register::Number(0),
        rs: Register::Number(0),
        rt: Register::Number(0),
        shift: Expr::Number(0)
    },
    "noop" => Instruction::RType {
        op_code: RTypeOp::Sll,
        rd: Register::Number(0),
        rs: Register::Number(0),
        rt: Register::Number(0),
        shift: Expr::Number(0)
    },
    PseudoInstruction => Instruction::Pseudo(<>),
};

PseudoInstruction: PseudoInstruction = {
    "li" <rd:Register> "," <value:Expr> => PseudoInstruction::LoadImmediate { rd, value },
    "la" <rd:Register> "," <label:Identifier> => PseudoInstruction::LoadAddress { rd, label },
    "move" <rt:Register> "," <rs:Register> => PseudoInstruction::Move { rt, rs },
};

RTypeOpRdRsRt: RTypeOp = {
    "add" => RTypeOp::Add,
    "addu" => RTypeOp::Addu,
    "and" => RTypeOp::And,
    "slt" => RTypeOp::Slt,
    "sltu" => RTypeOp::Sltu,
    "sub" => RTypeOp::Sub,
    "subu" => RTypeOp::Subu,
    "xor" => RTypeOp::Xor,
};

RTypeOpRdRtRs: RTypeOp = {
    "sllv" => RTypeOp::Sllv,
    "srlv" => RTypeOp::Srlv,
};

RTypeOpRdRtSh: RTypeOp = {
    "sll" => RTypeOp::Sll,
    "sra" => RTypeOp::Sra,
    "srl" => RTypeOp::Srl,
};

RTypeOpRsRt: RTypeOp = {
    "div" => RTypeOp::Div,
    "divu" => RTypeOp::Divu,
    "mult" => RTypeOp::Mult,
    "multu" => RTypeOp::Multu,
};

RTypeOpRs: RTypeOp = {
    "jr" => RTypeOp::Jr,
    "jalr" => RTypeOp::Jalr,
};

RTypeOpRd: RTypeOp = {
    "mfhi" => RTypeOp::Mfhi,
    "mflo" => RTypeOp::Mflo,
};

ITypeOpRtRs: ITypeOp = {
    "addi" => ITypeOp::Addi,
    "addiu" => ITypeOp::Addiu,
    "andi" => ITypeOp::Andi,
    "slti" => ITypeOp::Slti,
    "sltiu" => ITypeOp::Sltiu,
    "xori" => ITypeOp::Xori,
};

ITypeOpRsRt: ITypeOp = {
    "beq" => ITypeOp::Beq,
    "bne" => ITypeOp::Bne,
};

// The u8 is rt, which distinguishes some of the instructions
ITypeOpRs: (ITypeOp, u8) = {
    "bgez" => (ITypeOp::Bgez, 1),
    "bgezal" => (ITypeOp::Bgezal, 0x11),
    "bgtz" => (ITypeOp::Bgtz, 0),
    "blez" => (ITypeOp::Blez, 0),
    "bltz" => (ITypeOp::Bltz, 0),
    "bltzal" => (ITypeOp::Bltzal, 0x10),
};

StorageITypeOp: ITypeOp = {
    "sb" => ITypeOp::Sb,
    "sw" => ITypeOp::Sw,
    "lb" => ITypeOp::Lb,
    "lw" => ITypeOp::Lw,
};

JTypeOp: JTypeOp = {
    "j" => JTypeOp::Jump,
    "jal" => JTypeOp::Jal,
};

Register: Register = {
    "$" <Number> => Register::Number(<> as u8),
    "$" <Identifier> => Register::Name(<>),
};

match {
    // Ignore comments
    r"#[^\n]*" => { },

    // Ignore whitespace (but keep newlines)
    "\t" => { },
    " " => { },

    // Catch-all for the rest of the terminals
    _
}
